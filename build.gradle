// VisualInspectionOfCssAndJs/build.gradle
// $ gradle drivers

version = '0.6.0'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    materialstoreVersion = '0.6.2'
    kazurayam_FreeMarker_directivesVersion = '0.2.0'
    ExecutionProfilesLoaderVersion = "1.2.1"
    ashotVersion = '1.5.4'
    javadiffutilsVersion = '4.9'
    jsoupVersion = '1.7.2'
    freemarkerVersion = '2.3.31'
    cdtjavaclientVersion = '4.0.0'
    seleniumdevtoolsv96Version = '4.1.1'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
}

dependencies {
    myconf group: 'com.kazurayam', name: 'materialstore', version: "${materialstoreVersion}"
    myconf group: 'com.kazurayam', name: 'kazurayam_FreeMarker_directives', version: "${kazurayam_FreeMarker_directivesVersion}"
    myconf group: 'com.kazurayam', name: 'ExecutionProfilesLoader', version: "${ExecutionProfilesLoaderVersion}"
    myconf group: 'ru.yandex.qatools.ashot', name: 'ashot',
                    version: "${ashotVersion}"
    myconf group: 'io.github.java-diff-utils', name: 'java-diff-utils',
                    version: "${javadiffutilsVersion}"
    myconf group: 'org.jsoup', name: 'jsoup', version: "${jsoupVersion}"
    myconf group: 'org.freemarker', name: 'freemarker', version: "${freemarkerVersion}"
    myconf group: 'com.github.kklisura.cdt', name: 'cdt-java-client',
        version: "${cdtjavaclientVersion}"
    myconf group: 'org.seleniumhq.selenium', name: 'selenium-devtools-v96',
        version: "${seleniumdevtoolsv96Version}"
}

task drivers {
    doFirst {
        delete fileTree("Drivers") {
            include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
        }
    }
    doLast {
        copy { copySpec ->
            copySpec
                .from(project.getConfigurations().getByName("myconf"))
                .into("Drivers")
                .include(
                    "**/materialstore*.jar",
                    "**/kazurayam_FreeMarker_directives*.jar",
                    "**/ExecutionProfilesLoader*.jar",
                    "**/ashot*.jar",
                    "**/jsoup*.jar",
                    "**/java-diff-utils*.jar",
                    "**/freemarker*.jar",
                    "**/cdt-java-client*.jar",
                    "**/selenium-devtools-v96*.jar"
                    )
                .rename({ s ->
                    AUTO_IMPORTED_JAR_PREFIX + s
                    })
        }
    }
}
